# Create S3 bucket
resource "aws_s3_bucket" "this" {
  bucket = var.bucket_name

  force_destroy = true

  tags = {
    Name = var.bucket_name
  }
}

# Block public access
resource "aws_s3_bucket_public_access_block" "this" {
  bucket = aws_s3_bucket.this.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

# Enable versioning
resource "aws_s3_bucket_versioning" "this" {
  bucket = aws_s3_bucket.this.id

  versioning_configuration {
    status = "Enabled"
  }
}

# Enable SSE encryption

resource "aws_s3_bucket_server_side_encryption_configuration" "this" {
  bucket = aws_s3_bucket.this.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}


# Grant ECS task permission to use the bucket
resource "aws_iam_policy" "s3_policy" {
  name        = "${var.bucket_name}-policy"
  description = "S3 access for Outline ECS tasks"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "s3:PutObject",
          "s3:GetObject",
          "s3:DeleteObject",
          "s3:ListBucket"
        ]
        Resource = [
          "arn:aws:s3:::${var.bucket_name}",
          "arn:aws:s3:::${var.bucket_name}/*"
        ]
      }
    ]
  })
}

# Attach policy to ECS role
resource "aws_iam_role_policy_attachment" "attach_s3_policy" {
  role       = var.ecs_task_role_name
  policy_arn = aws_iam_policy.s3_policy.arn
}

